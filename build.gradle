buildscript {    // Configuration for building
  ext {
    appenginePluginVersion = '1.3.5'
  }
  repositories {
    jcenter()    // Bintray's repository - a fast Maven Central mirror & more
    mavenCentral()
  }
  dependencies {
    classpath "com.google.cloud.tools:appengine-gradle-plugin:${appenginePluginVersion}"    // Latest 1.x.x release
  }
}

repositories {   // repositories for Jar's you access in your code
  maven {
    url 'https://oss.sonatype.org/content/repositories/snapshots' // SNAPSHOT repository (if needed)
  }
  mavenCentral()
  jcenter()
}

apply plugin: 'java'                              // standard Java tasks
apply plugin: 'war'                               // standard Web Archive plugin
apply plugin: 'com.google.cloud.tools.appengine'  // App Engine tasks

dependencies {

  ext {
    appengineSdkVersion = '1.9.64'
    springVersion = '5.0.9.RELEASE'
  }

  compile "com.google.appengine:appengine-api-1.0-sdk:${appengineSdkVersion}"  // Latest App Engine Api's
  providedCompile "javax.servlet:javax.servlet-api:3.1.0"

  compile group: 'jstl', name: 'jstl', version: '1.2'
  compile group: 'org.json',name: 'json', version: '20180813'
  compile group: 'joda-time',name: 'joda-time', version: '2.10'
  compile group: 'com.google.appengine.tools', name: 'appengine-gcs-client', version: '0.8'
  compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.3'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'com.google.truth', name: 'truth', version: '0.33'
  testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

  testCompile "com.google.appengine:appengine-testing:${appengineSdkVersion}"
  testCompile "com.google.appengine:appengine-api-stubs:${appengineSdkVersion}"
  testCompile "com.google.appengine:appengine-tools-sdk:${appengineSdkVersion}"
}

// Always run unit tests
appengineDeploy.dependsOn test
appengineStage.dependsOn test

appengine {  // App Engine tasks configuration
  deploy {   // deploy configuration
    stopPreviousVersion = true  // default - stop the current version
    promote = true // default - makes above version default
    version = "gdg"
  }
}

test {
  useJUnit()
  testLogging.showStandardStreams = true
  beforeTest { descriptor ->
     logger.lifecycle("test: " + descriptor + "  Running")
  }

  onOutput { descriptor, event ->
     logger.lifecycle("test: " + descriptor + ": " + event.message )
  }
  afterTest { descriptor, result ->
    logger.lifecycle("test: " + descriptor + ": " + result )
  }
}

group   = "org.sen.webapp"        // Generated output GroupId
version = "1.0-SNAPSHOT"       // Version in generated output

sourceCompatibility = 1.8     // App Engine Flexible uses Java 8
targetCompatibility = 1.8     // App Engine Flexible uses Java 8